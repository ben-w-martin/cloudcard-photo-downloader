set curlUrl=https://api.onlinephotosubmission.com/api/reports/people
set authToken=
set outputDir=
set photoDir=

FOR /f %%a IN ('WMIC OS GET LocalDateTime ^| FIND "."') DO SET DTS=%%a
SET DateTime=%DTS:~0,4%-%DTS:~4,2%-%DTS:~6,2%_%DTS:~8,2%-%DTS:~10,2%-%DTS:~12,2%

(for %%f in ("%photoDir%\*.jpg") do @echo "%%~nf") >> quotesVar.temp

@echo off
setlocal EnableDelayedExpansion

set "line="
(  for /F "delims=" %%a in (quotesVar.temp) do (
      if defined line echo !line!,
      set "line=%%a"
   )
   echo !line!
) > commaVar.temp

echo { "include": [> request-body.json
type commaVar.temp>> request-body.json
echo ] }>> request-body.json

curl --location --request POST "%curlUrl%" ^
 --header "X-Auth-Token: %authToken%" ^
 --header "Accept: text/csv" ^
 --header "Content-Type: application/json" ^
 --data "@request-body.json" ^
 --output "%outputDir%"\downloaded-photo-info_%DateTime%.csv

del quotesVar.temp
del commaVar.temp
del request-body.json
rem del "%photoDir%\*.jpg"
